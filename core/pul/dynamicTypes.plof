/*
 * Dynamic type annotations for Plof
 *
 *  Copyright (c) 2007, 2008  Gregor Richards
 *  
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to
 *  deal in the Software without restriction, including without limitation the
 *  rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 *  sell copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *  
 *  The above copyright notice and this permission notice shall be included in
 *  all copies or substantial portions of the Software.
 *  
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 *  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 *  IN THE SOFTWARE.
 */

// typechecking operators
plofbnf {
    plof_combine => plof_is;

    plof_is = plof_is "is" plof_is_next => plof {
        $0.opIs($2)
    }
    plof_is = plof_is_next => plof { $0 }
    plof_is => plof_as;

    plof_as = plof_as "as" plof_as_next => plof {
        $0.opAs($2)
    }
    plof_as = plof_as_next => plof { $0 }
};

// typechecking in Object
Object := [
    opIs = (type) {
        psl {
            plof{this} pul_eval "__pul_type" member
            0 integer
            2 integer array

            // go through each element
            {
                // arg: [pul type, index]

                // are we too far?
                push0 0 integer index length
                push1 1 integer index
                {
                    // Ran out. This is not that
                    plof{return False} pul_eval
                }
                {
                    // check this one
                    push0 0 integer index
                    push1 1 integer index index
                    plof{type} pul_eval
                    {
                        // they're equal, this matches
                        plof{return True} pul_eval
                    }
                    {
                        // no match
                    } cmp
                } lte

                // increment the index
                push0
                1 integer
                    push1 1 integer index
                        1 integer add
                indexset

                // and loop
                loop
            } call
        }
    };

    opAs = (type) {
        if (this is type) (
            // simple case
            return this;
        ) else if (!((var ret = this.opCastTo(type)) is Null)) (
            return ret;
        ) else if (!((var ret = type.opCastFrom(this)) is Null)) (
            return ret;
        ) else (
            return Null;
        );
    };

    opCastTo = (type) {
        Null
    };

    opCastFrom = (obj) {
        Null
    };
];

plofbnf {
    // specified argument types
    plof_param = plof_identifier "as" plof_semicolon_next => {
        // still get the named param as usual
        {push1}
        push1 0 integer index concat

        // store the astype for later
        push1 2 integer index concat
        {
            this "astype" push2 memberset
        } concat

        {
            pul_eval
            1 integer array

            // stack: args fcontext index fcontext ident astype
            push5 push4 index

            // stack: args fcontext index fcontext ident astype arg
            this "arg" push2 memberset

            // now run typechecking/coersion
            pul_eval "opAs" pul_varwrap pul_eval call pul_eval

            // stack: args fcontext index fcontex ident arg?

            // check if it's null (failed to coerce)
            push0 plof{Null} pul_eval
            {
                // whoops, type mismatch
                plof {
                    throw (new TypeMismatchException(arg, astype));
                } pul_eval
            }
            {} cmp

            // otherwise, set as usual
            memberset
            1 integer add
        } concat
    }
};
