/*
 * The Plof User Language basic definitions. This file gets you from the grammar defined in base.psl
 * to a functional PUL grammar.
 *
 *  Copyright (c) 2007, 2008, 2009, 2010 Gregor Richards
 *  
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to
 *  deal in the Software without restriction, including without limitation the
 *  rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 *  sell copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *  
 *  The above copyright notice and this permission notice shall be included in
 *  all copies or substantial portions of the Software.
 *  
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 *  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 *  IN THE SOFTWARE.
 */

// Whitespace but no newlines, used to distinguish "complete" statements from incomplete ones
__grammar_add(nnlwhite,
              /(([ \t\r]*(\/\*([^\*]*\*[^\/])*[^\*]*\*\/)?(\\\n)?)*)/,
              {});

// End of statement can be either a semicolon or a newline (perhaps with line comments)
__grammar_add(eos, nnlwhite /;/ white, {});
__grammar_add(eos, nnlwhite /((\/\/[^\r\n]*\r?)?\n)/ white, {});
//__grammar_add(eos, nnlwhite /\n/ white, {});

// Basic PSL: psl { psl code here };
__grammar_add(top, psl, push0 0 index);
__grammar_add(psl,
              white /psl/ token white /\{/ white pslOps /\}/ eos,
              push0 6 index);

// PSL BNF: grammar { production = target => { code } };
__grammar_add(top, grammar, push0 0 index);
__grammar_add(grammar,
              white /grammar/ token white /\{/ white grammar_productions /\}/ eos,
              push0 6 index {[gcommit]} concat);
__grammar_add(grammar_productions,
              grammar_productions grammar_production,
              push0 0 index push1 1 index concat);
__grammar_add(grammar_productions,
              grammar_production,
              push0 0 index);
__grammar_add(grammar_production,
              /[A-Za-z_][A-Za-z0-9_]*/ white /=/ white pslbnf_targets
                  /=>/ white /\{/ white pslOps /\}/ white,
              push0 0 index
                  push1 4 index
                  push2 9 index cwrap
                  {gadd}
                  concat concat concat iwrap);
__grammar_add(grammar_production,
              /[A-Za-z_][A-Za-z0-9_]*/ white /=>/ white /[A-Za-z_][A-Za-z0-9_]*/ eos,

              // set that->next to this->next, then this->next to that

              {global} "grammar_" null push3 0 index call concat "_next" concat swrap concat {member} concat
              // inner stack: global.grammar_<this>_next
              {global} concat "grammar_" null push3 4 index call concat "_next" concat swrap concat
              // inner stack: global.grammar_<this>_next global "grammar_<that>_next"
              {push2 memberset} concat
              // inner stack: global.grammar_<this>_next

              null push2 4 index call "_next" concat swrap concat
              {push0 grem} concat
              // inner stack: global.grammar_<this>_next "<that>_next"
              {push1 1 array {push0 0 index} gadd pop} concat
              // inner stack:

              // now this->next = that
              push1 4 index concat
              // inner stack: "<that>"
              {global} concat "grammar_" null push3 0 index call concat "_next" concat swrap concat
              // inner stack: "<that>" global "grammar_<this>_next"
              {push2 memberset} concat
              // inner stack: "<that>"

              null push2 0 index call "_next" concat swrap concat
              {push0 grem} concat
              // inner stack: "<that>" "<that>_next"
              {push1 1 array {push0 0 index} gadd pop} concat
              // inner stack:

              // wrap it all up in an immediate
              iwrap);
__grammar_add(pslbnf_targets,
              pslbnf_targets pslbnf_target,
              push0 0 index
                  push1 1 index
                  {aconcat}
                  concat concat);
__grammar_add(pslbnf_targets,
              pslbnf_target,
              push0 0 index);
__grammar_add(pslbnf_target,
              /\!?[A-Za-z_][A-Za-z0-9_]*/ token white,
              push0 0 index
                  {1 array}
                  concat);
__grammar_add(pslbnf_target,
              /(\/([^\\\/]*\\.)*[^\\\/]*\/)/ token white,
              push0 0 index
                  {1 array}
                  concat);

__grammar_add(pslbnf_target,
              /"/ /[A-Za-z0-9_]+/ /"/ white,
              // force this to be its own token production in the grammar
              null push1 1 index call

              // get the name of the production
              "token_" push1 concat swrap

              // remove it if it already exists
              {push0 push0 grem} concat

              // now make the regex
              "/"
              push2 concat
              "/" concat
              swrap concat

              // and gadd it
              {
                  "token" "nnlwhite" 3 array
                  {push0 0 index}
                  gadd
                  1 array
              } concat);

__grammar_add(pslbnf_target,
              /"/ /([^A-Za-z0-9_]([^\\\/"]*\\.)*[^\\\/"]*)/ /"/ white,
              // force this to be its own token production in the grammar
              null push1 1 index call

              // get the name of the production
              "token_" push1 concat swrap

              // remove it if it already exists
              {push0 push0 grem} concat

              // now make the regex
              "/"
              push2 concat
              "/" concat
              swrap concat

              // and gadd it
              {
                  "nnlwhite" 2 array
                  {push0 0 index}
                  gadd
                  1 array
              } concat);


__grammar_add(pslbnf_target,
              /"/ /[A-Za-z0-9_]+/ /"n/ white,
              // force this to be its own token production in the grammar
              null push1 1 index call

              // get the name of the production
              "tokenw_" push1 concat swrap

              // remove it if it already exists
              {push0 push0 grem} concat

              // now make the regex
              "/"
              push2 concat
              "/" concat
              swrap concat

              // and gadd it
              {
                  "token" "white" 3 array
                  {push0 0 index}
                  gadd
                  1 array
              } concat);

__grammar_add(pslbnf_target,
              /"/ /([^A-Za-z0-9_]([^\\\/"]*\\.)*[^\\\/"]*)/ /"n/ white,
              // force this to be its own token production in the grammar
              null push1 1 index call

              // get the name of the production
              "tokenw_" push1 concat swrap

              // remove it if it already exists
              {push0 push0 grem} concat

              // now make the regex
              "/"
              push2 concat
              "/" concat
              swrap concat

              // and gadd it
              {
                  "white" 2 array
                  {push0 0 index}
                  gadd
                  1 array
              } concat);

__grammar_commit();

// some convenient pseudo-ops
grammar {
    pslOp = "pul_eval" => {
        {global "__pul_eval" member call}
    }

    pslOp = "pul_forceEval" => {
        {global "__pul_forceEval" member call}
    }

    pslOp = "pul_set" => {
        {2 array global "__pul_set" member call pop}
    }

    pslOp = "pul_funcwrap" => {
        {global "__pul_funcwrap" member call}
    }

    /* mark this code as something which should be pul_funcwrap'd, but might be
     * extracted if we're just going to add a pul_eval to it */
    pslOp = "pul_apply_funcwrap" => {
        {
            push0 "isfuncwrap" member null
            {
                // not already a funcwrap, so wrap it
                push0 cwrap {global "__pul_funcwrap" member call} concat

                push0 "orig" push3 memberset
                push0 "isfuncwrap" new memberset
            }
            {
                // already a funcwrap, don't need to do anything
            } cmp
        }
    }

    // apply an eval unless this is a funcwrap, in which case just use it directly
    pslOp = "pul_apply_eval" => {
        {
            push0 "isfuncwrap" member null
            {
                // is not a funcwrap, just use it and eval it
                {global "__pul_eval" member call} concat
            }
            {
                // it is a funcwrap, get out the version before wrapping. Still need to call pul_eval because it's recursive
                "orig" member {global "__pul_eval" member call} concat
            } cmp
        }
    }

    pslOp = "pul_varwrap" => {
        {2 array global "__pul_varwrap" member call}
    }

    pslOp = "pul_parentwrap" => {
        {global "__pul_parentwrap" member call}
    }

    pslOp = "pul_fcontext" => {
        {this global "__pul_fcontext" member call}
    }

    pslOp = "pul_fcontext_of" => {
        {global "__pul_fcontext" member call}
    }
}

// the base PUL grammar
grammar {
    top = white top_next => { push0 1 index }
    top => plof_statement

    plof_statement = plof_statement_next eos => {
        push0 0 index pul_apply_eval {pop} concat
    }
    plof_statement => plof_assign

    // allow Plof in PSL
    pslOp = "plof"n "\{"n plof_semicolon "\}"n => {
        push0 2 index
    }

    plof_semicolon = plof_semicolon eos plof_semicolon_next => {
        push0 0 index pul_apply_eval {pop} concat
        push1 2 index concat
        pul_apply_funcwrap
    }
    plof_semicolon = plof_semicolon eos => {
        push0 0 index
    }
    plof_semicolon = plof_semicolon_next => { push0 0 index }
    plof_semicolon => plof_assign

    plof_assign = plof_assign_next "="n plof_assign => {
        push0 2 index pul_apply_eval
        push1 0 index concat
        {push1 pul_set} concat
        pul_apply_funcwrap
    }
    plof_assign = plof_assign_next => { push0 0 index }
    plof_assign => plof_bind

    plof_bind = "let"n plof_bind_next "="n plof_bind => {
        push0 3 index
        push1 1 index concat
        {push1 pul_set} concat
        pul_apply_funcwrap
    }
    plof_bind = plof_bind_next => { push0 0 index }
    plof_bind => plof_group

    plof_group = "forceEval"n "\("n plof_identifier "\)" => {
        push0 2 index
        {this
         push1
         global 3 array this "opMember" resolve member pul_eval call

         push1 member

         pul_forceEval} concat
        //pul_apply_funcwrap
        cwrap { pul_funcwrap } concat
    }

    plof_group = plof_group_next => { push0 0 index }
    plof_group => plof_member

    plof_member = plof_member "\."n plof_identifier => {
        push0 0 index pul_apply_eval
        push1 2 index concat
        {pul_varwrap} concat
        pul_apply_funcwrap
    }
    plof_member = plof_member "\."n "parent" => {
        push0 0 index pul_apply_eval
        {pul_parentwrap} concat
        pul_apply_funcwrap
    }
    plof_member = plof_member_next => { push0 0 index }
    plof_member => plof_parens

    plof_parens = "\("n plof_semicolon "\)" => {
        push0 1 index
    }
    plof_parens = plof_parens_next => { push0 0 index }
    plof_parens => plof_literal

    // none of these literals are actually literals (hm), but they're at the same precedence
    plof_literal = "psl"n "\{"n pslOps "\}" => {
        push0 2 index
        pul_apply_funcwrap
    }
    plof_literal = /\$/ number nnlwhite => {
        null push1 1 index call
        byte
        "ü" wrap
    }
    plof_literal = plof_literal_next => { push0 0 index }
    plof_literal => plof_var

    plof_var = "var"n plof_identifier => {
        {pul_fcontext}
        push1 1 index concat
        {push1 push1 this "Null" resolve member memberset pul_varwrap} concat
        pul_apply_funcwrap
    }
    plof_var = plof_var_next => { push0 0 index }
    plof_var => plof_ident

    plof_ident = plof_identifier => {
        // call the global opMember with the context and name, which will return a new context
        push0 0 index
        {
            this
            push1
            global /* opMember's third argument specifies whether to continue
                    * searching out of this scope */
            3 array
            this "opMember" resolve member pul_eval call

            push1 pul_varwrap
        } concat

        // wrap it up
        // pul_apply_funcwrap FIXME: can't apply_wrap because it pushes too many
        cwrap { pul_funcwrap } concat
    }

    plof_identifier = !plof_keyword /[A-Za-z_][A-Za-z0-9_]*/ token nnlwhite => {
        push0 1 index
    }

    plof_keyword = "as" => {}
    plof_keyword = "by" => {}
    plof_keyword = "forceEval" => {}
    plof_keyword = "is" => {}
    plof_keyword = "import" => {}
    plof_keyword = "in" => {}
    plof_keyword = "include" => {}
    plof_keyword = "parent" => {}
    plof_keyword = "return" => {}
    plof_keyword = "rtInclude" => {}
    plof_keyword = "to" => {}
    plof_keyword = "var" => {}
}

// now plofbnf, trivial and removal
grammar {
    grammar_production =
        /[A-Za-z_][A-Za-z0-9_]*/ white /=/ white
        pslbnf_targets /=>/ white
        "plof" /\{/ white plof_semicolon /\}/ white => {

        // push the name and targets
        push0 0 index
        push1 4 index concat

        // add needless members since our replacement is useless
        { {} {} {} {} {} {} {} {} {} {} 10 array aconcat }

        // parse the code ...
        push2 10 index

        push0 "isfuncwrap" member null
        {
            cwrap
            {
                push1 replace
            } concat
        }
        {
            "orig" member
            cwrap
            {
                push1 replace
                pul_apply_funcwrap
            } concat
        } cmp

        concat cwrap concat

        // gadd the whole thing
        {gadd} concat

        // make it immediate
        iwrap
    }

    grammar_production = /!/ white /[A-Za-z_][A-Za-z0-9_]*/ eos => {
        // remove an element
        push0 1 index
        {grem} concat

        // make it immediate
        iwrap
    }
}

// Integers
grammar {
    plof_literal = number nnlwhite => {
        push0 0 index
        {
            // call opInteger to wrap it up
            1 array
            this "opInteger" resolve member call
        } concat

        pul_apply_funcwrap
    }
}

// Basic object definition
grammar {
    plof_bind => plof_combine

    plof_assign = plof_assign_next ":="n "\["n plof_obj_defs "\]" => {
        // get the left
        push0 0 index pul_apply_eval
        {
            push0
        } concat

        // then run the extender
        push1 3 index cwrap concat

        {
            // stack: obj obj extender
            push0 push2 parentset call pop
        } concat

        // wrap it up
        pul_apply_funcwrap
    }

    plof_combine = plof_combine ":"n "\["n plof_obj_defs "\]" => {
        // call opDuplicate
        push0 0 index pul_apply_eval
        {
            1 array
            this "opDuplicate" resolve member call
            push0 pul_fcontext parentset
            push0
        } concat

        // then run the extender
        push1 3 index cwrap concat
        {
            // stack: old new new extender
            push0 push2 parentset call pop
        } concat

        // wrap
        pul_apply_funcwrap
    }
    plof_combine = plof_combine ":"n plof_combine_next => {
        // combine them
        push0 0 index pul_apply_eval
        push1 2 index pul_apply_eval concat
        {
            // call opCombine
            2 array
            this "opCombine" resolve member call
            // FIXME: This should parent, but that causes issues
        } concat

        // wrap
        pul_apply_funcwrap
    }
    plof_combine = plof_combine_next => {
        push0 0 index
    }

    plof_obj_defs = plof_obj_defs eos plof_obj_defs_next => {
        push0 0 index
        push1 2 index concat
    }
    plof_obj_defs = plof_obj_defs eos => {
        push0 0 index
    }
    plof_obj_defs = plof_obj_defs_next => {
        push0 0 index
    }
    plof_obj_defs = white => {
        {}
    }
    plof_obj_defs => plof_obj_def

    plof_obj_def = plof_identifier "="n plof_bind_next => {
        {push0}
        push1 0 index concat
        push1 2 index pul_apply_eval concat
        {push0 push4 parentset memberset} concat
    }
}
