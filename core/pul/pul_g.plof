/*
 * The Plof User Language basic definitions. This file gets you from the grammar defined in base.psl
 * to a functional PUL grammar, and creates mostly-empty Object and Function objects. object.pul
 * should be loaded next to flesh out the important objects.
 *
 *  Copyright (c) 2007, 2008, 2009  Gregor Richards
 *  
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to
 *  deal in the Software without restriction, including without limitation the
 *  rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 *  sell copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *  
 *  The above copyright notice and this permission notice shall be included in
 *  all copies or substantial portions of the Software.
 *  
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 *  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 *  IN THE SOFTWARE.
 */

// Basic PSL: psl { psl code here };
__grammar_add(top, psl, push0 0 integer index);
__grammar_add(psl,
              white /psl/ token white /\{/ white pslOps /\}/ white /;/ white,
              push0 6 integer index);

// PSL BNF: pslbnf { production = target => { code } };
__grammar_add(top, pslbnf, push0 0 integer index);
__grammar_add(pslbnf,
              white /pslbnf/ token white /\{/ white pslbnf_productions /\}/ white /;/ white,
              push0 6 integer index {[gcommit]} concat);
__grammar_add(pslbnf_productions,
              pslbnf_productions pslbnf_production,
              push0 0 integer index push1 1 integer index concat);
__grammar_add(pslbnf_productions,
              pslbnf_production,
              push0 0 integer index);
__grammar_add(pslbnf_production,
              /[A-Za-z_][A-Za-z0-9_]*/ white /=/ white pslbnf_targets
                  /=>/ white /\{/ white pslOps /\}/ white,
              push0 0 integer index
                  push1 4 integer index
                  push2 9 integer index cwrap
                  {gadd}
                  concat concat concat iwrap);
__grammar_add(pslbnf_production,
              /[A-Za-z_][A-Za-z0-9_]*/ white /=>/ white /[A-Za-z_][A-Za-z0-9_]*/ white /;/ white,

              // set that->next to this->next, then this->next to that

              {global} "grammar_" null push3 0 integer index call concat "_next" concat swrap concat {member} concat
              // inner stack: global.grammar_<this>_next
              {global} concat "grammar_" null push3 4 integer index call concat "_next" concat swrap concat
              // inner stack: global.grammar_<this>_next global "grammar_<that>_next"
              {push2 memberset} concat
              // inner stack: global.grammar_<this>_next

              null push2 4 integer index call "_next" concat swrap concat
              {push0 grem} concat
              // inner stack: global.grammar_<this>_next "<that>_next"
              {push1 1 integer array {push0 0 integer index} gadd pop} concat
              // inner stack:

              // now this->next = that
              push1 4 integer index concat
              // inner stack: "<that>"
              {global} concat "grammar_" null push3 0 integer index call concat "_next" concat swrap concat
              // inner stack: "<that>" global "grammar_<this>_next"
              {push2 memberset} concat
              // inner stack: "<that>"

              null push2 0 integer index call "_next" concat swrap concat
              {push0 grem} concat
              // inner stack: "<that>" "<that>_next"
              {push1 1 integer array {push0 0 integer index} gadd pop} concat
              // inner stack:

              // wrap it all up in an immediate
              iwrap);
__grammar_add(pslbnf_targets,
              pslbnf_targets pslbnf_target,
              push0 0 integer index
                  push1 1 integer index
                  {aconcat}
                  concat concat);
__grammar_add(pslbnf_targets,
              pslbnf_target,
              push0 0 integer index);
__grammar_add(pslbnf_target,
              /[A-Za-z_][A-Za-z0-9_]*/ token white,
              push0 0 integer index
                  {1 integer array}
                  concat);
__grammar_add(pslbnf_target,
              /(\/([^\\\/]*\\.)*[^\\\/]*\/)/ token white,
              push0 0 integer index
                  {1 integer array}
                  concat);

__grammar_add(pslbnf_target,
              /"/ /[A-Za-z0-9_]+/ /"/ white,
              // force this to be its own token production in the grammar
              null push1 1 integer index call

              // get the name of the production
              "token_" push1 concat swrap

              // remove it if it already exists
              {push0 push0 grem} concat

              // now make the regex
              "/"
              push2 concat
              "/" concat
              swrap concat

              // and gadd it
              {
                  "token" "white" 3 integer array
                  {push0 0 integer index}
                  gadd
                  1 integer array
              } concat);

__grammar_add(pslbnf_target,
              /"/ /([^A-Za-z0-9_]([^\\\/"]*\\.)*[^\\\/"]*)/ /"/ white,
              // force this to be its own token production in the grammar
              null push1 1 integer index call

              // get the name of the production
              "token_" push1 concat swrap

              // remove it if it already exists
              {push0 push0 grem} concat

              // now make the regex
              "/"
              push2 concat
              "/" concat
              swrap concat

              // and gadd it
              {
                  "white" 2 integer array
                  {push0 0 integer index}
                  gadd
                  1 integer array
              } concat);

__grammar_commit();

// some convenient pseudo-ops
pslbnf {
    pslOp = "pul_eval" => {
        {global "__pul_eval" member call}
    }

    pslOp = "pul_forceEval" => {
        {global "__pul_forceEval" member call}
    }

    pslOp = "pul_set" => {
        {2 integer array global "__pul_set" member call pop}
    }

    pslOp = "pul_funcwrap" => {
        {global "__pul_funcwrap" member call}
    }

    /* mark this code as something which should be pul_funcwrap'd, but might be
     * extracted if we're just going to add a pul_eval to it */
    pslOp = "pul_apply_funcwrap" => {
        {
            push0 "isfuncwrap" member null
            {
                // not already a funcwrap, so wrap it
                push0 cwrap {global "__pul_funcwrap" member call} concat

                push0 "orig" push3 memberset
                push0 "isfuncwrap" new memberset
            }
            {
                // already a funcwrap, don't need to do anything
            } cmp
        }
    }

    // apply an eval unless this is a funcwrap, in which case just use it directly
    pslOp = "pul_apply_eval" => {
        {
            push0 "isfuncwrap" member null
            {
                // is not a funcwrap, just use it and eval it
                {global "__pul_eval" member call} concat
            }
            {
                // it is a funcwrap, get out the version before wrapping. Still need to call pul_eval because it's recursive
                "orig" member {global "__pul_eval" member call} concat
            } cmp
        }
    }

    pslOp = "pul_varwrap" => {
        {2 integer array global "__pul_varwrap" member call}
    }

    pslOp = "pul_parentwrap" => {
        {global "__pul_parentwrap" member call}
    }

    pslOp = "pul_fcontext" => {
        {this global "__pul_fcontext" member call}
    }
};

// the base PUL grammar
pslbnf {
    top = white top_next => { push0 1 integer index }
    top => plof_statement;

    plof_statement = plof_statement_next ";" => {
        push0 0 integer index pul_apply_eval {pop} concat
    }
    plof_statement => plof_assign;

    // allow Plof in PSL
    pslOp = "plof" "\{" plof_semicolon "\}" => {
        push0 2 integer index
    }

    plof_semicolon = plof_semicolon ";" plof_semicolon_next => {
        push0 0 integer index pul_apply_eval {pop} concat
        push1 2 integer index concat
        pul_apply_funcwrap
    }
    plof_semicolon = plof_semicolon ";" => {
        push0 0 integer index pul_apply_eval {pop this "Null" resolve member} concat
        pul_apply_funcwrap
    }
    plof_semicolon = plof_semicolon_next => { push0 0 integer index }
    plof_semicolon => plof_assign;

    plof_assign = plof_assign_next "=" plof_assign => {
        push0 2 integer index pul_apply_eval
        push1 0 integer index concat
        {push1 pul_set} concat
        pul_apply_funcwrap
    }
    plof_assign = plof_assign_next => { push0 0 integer index }
    plof_assign => plof_bind;

    plof_bind = "let" plof_bind_next "=" plof_bind => {
        push0 3 integer index
        push1 1 integer index concat
        {push1 pul_set} concat
        pul_apply_funcwrap
    }
    plof_bind = plof_bind_next => { push0 0 integer index }
    plof_bind => plof_group;

    plof_group = "forceEval" "\(" plof_group "\)" => {
        push0 2 integer index
        {pul_forceEval} concat
        pul_apply_funcwrap
    }

    plof_group = plof_group "\." plof_identifier => {
        push0 0 integer index pul_apply_eval
        push1 2 integer index concat
        {pul_varwrap} concat
        pul_apply_funcwrap
    }
    plof_group = plof_group "\." "parent" => {
        push0 0 integer index pul_apply_eval
        {pul_parentwrap} concat
        pul_apply_funcwrap
    }
    plof_group = plof_group_next => { push0 0 integer index }
    plof_group => plof_parens;

    plof_parens = "\(" plof_semicolon "\)" => {
        push0 1 integer index
    }
    plof_parens = plof_parens_next => { push0 0 integer index }
    plof_parens => plof_literal;

    // none of these literals are actually literals (hm), but they're at the same precedence
    plof_literal = "psl" "\{" pslOps "\}" => {
        push0 2 integer index
        pul_apply_funcwrap
    }
    plof_literal = /\$/ number => {
        null push1 1 integer index call
        integer byte
        "ü" wrap
    }
    plof_literal = plof_literal_next => { push0 0 integer index }
    plof_literal => plof_var;

    plof_var = "var" plof_identifier => {
        {pul_fcontext}
        push1 1 integer index concat
        {push1 push1 this "Null" resolve member memberset pul_varwrap} concat
        pul_apply_funcwrap
    }
    plof_var = plof_var_next => { push0 0 integer index }
    plof_var => plof_ident;

    plof_ident = plof_identifier => {
        // the name we're actually looking for
        push0 0 integer index

        // use opMember if possible
        {
            "opMember"

            // look in the current function context
            this push2 push2 2 integer array resolve
            4 integer array

            // now check whether we used opMember or the name
            push0 1 integer index
            push1 3 integer index
            {
                // used opMember: args = [name, next context]
                push0 0 integer index
                push1 2 integer index parent 2 integer array
                push1 2 integer index "opMember" member pul_eval call
            }
            {
                // no opMember
                push0 2 integer index
                push1 0 integer index
                pul_varwrap
            } cmp
        } concat

        // wrap it up
        pul_apply_funcwrap
    }

    plof_identifier = plof_notkeyword /[A-Za-z_][A-Za-z0-9_]*/ token white => {
        push0 1 integer index
    }

    plof_notkeyword =
        /()(?!as[^A-Za-z0-9_])/
        /()(?!by[^A-Za-z0-9_])/
        /()(?!forceEval[^A-Za-z0-9_])/
        /()(?!is[^A-Za-z0-9_])/
        /()(?!in[^A-Za-z0-9_])/
        /()(?!include[^A-Za-z0-9_])/
        /()(?!parent[^A-Za-z0-9_])/
        /()(?!return[^A-Za-z0-9_])/
        /()(?!rtInclude[^A-Za-z0-9_])/
        /()(?!to[^A-Za-z0-9_])/
        /()(?!var[^A-Za-z0-9_])/
        => {{}}
};

// now plofbnf
pslbnf {
    top = white plofbnf => { push0 1 integer index }

    plofbnf = "plofbnf" /\{/ white plofbnf_productions /\}/ white /;/ white => {
        push0 3 integer index
        {[gcommit]} concat
    }

    plofbnf_productions = plofbnf_productions plofbnf_production => {
        push0 0 integer index
        push1 1 integer index concat
    }
    plofbnf_productions = plofbnf_production => {
        push0 0 integer index
    }

    plofbnf_production = pslbnf_production => { push0 0 integer index }
    plofbnf_production =
        /[A-Za-z_][A-Za-z0-9_]*/ white /=/ white
        pslbnf_targets /=>/ white
        "plof" /\{/ white plof_semicolon /\}/ white => {

        // push the name and targets
        push0 0 integer index
        push1 4 integer index concat

        // add needless members since our replacement is useless
        { {} {} {} {} {} {} {} {} {} {} 10 integer array aconcat }

        // parse the code ...
        push2 10 integer index

        push0 "isfuncwrap" member null
        {
            cwrap
            {
                push1 replace
            } concat
        }
        {
            "orig" member
            cwrap
            {
                push1 replace
                pul_apply_funcwrap
            } concat
        } cmp

        concat cwrap concat

        // gadd the whole thing
        {gadd} concat

        // make it immediate
        iwrap
    }
};

// Integers
pslbnf {
    plof_literal = number => {
        push0 0 integer index
        {
            // call opInteger to wrap it up
            integer
            1 integer array
            this "opInteger" resolve member call
        } concat

        pul_apply_funcwrap
    }
};

// Basic object definition
pslbnf {
    plof_bind => plof_combine;

    plof_assign = plof_assign_next ":=" "\[" plof_obj_defs "\]" => {
        // get the left
        push0 0 integer index pul_apply_eval

        // then run the extender
        push1 3 integer index concat

        // wrap it up
        pul_apply_funcwrap
    }

    plof_combine = plof_combine ":" "\[" plof_obj_defs "\]" => {
        // call opDuplicate
        push0 0 integer index pul_apply_eval
        {
            1 integer array
            this "opDuplicate" resolve member call
            push0
        } concat

        // then run the extender
        push1 3 integer index cwrap concat
        {
            // stack: old new new extender
            push0 push2 parentset call pop
        } concat

        // wrap
        pul_apply_funcwrap
    }
    plof_combine = plof_combine ":" plof_combine_next => {
        // combine them
        push0 0 integer index pul_apply_eval
        push1 2 integer index pul_apply_eval concat
        {
            push1 push1 combine

            // stack: old1 old2 new

            // then call opCombine
            3 integer array
            push0 this "opCombine" resolve member call
        } concat

        // wrap
        pul_apply_funcwrap
    }
    plof_combine = plof_combine_next => {
        push0 0 integer index
    }

    plof_obj_defs = plof_obj_defs plof_obj_defs_next => {
        push0 0 integer index
        push1 1 integer index concat
    }
    plof_obj_defs = plof_obj_defs_next => {
        push0 0 integer index
    }
    plof_obj_defs = white => {
        {}
    }
    plof_obj_defs => plof_obj_def;

    plof_obj_def = plof_identifier "=" plof_bind_next ";" => {
        {push0}
        push1 0 integer index concat
        push1 2 integer index pul_apply_eval concat
        {push0 push4 parentset memberset} concat
    }
};
